; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5D0411E3-47F5-441A-B595-C1DCE87519C2}}
AppName=Cloud Portal Office Desktop
AppVersion=0.1.12.1
AppPublisher=SHARP
AppPublisherURL=http://www.sharp.com/
AppSupportURL=http://www.sharp.com/
AppUpdatesURL=http://www.sharp.com/

LicenseFile=Cloud Portal Office Desktop\data\CloudDesk_EULA.txt
InfoAfterFile=InfoAfterInstall.txt
UsePreviousAppDir=no
DefaultDirName={pf}\SHARP\Cloud Portal Office Desktop
DefaultGroupName=Cloud Portal Office Desktop
OutputDir=dist
OutputBaseFilename=CPODesktop-0.1.12.1-Win32-setup
SetupIconFile=Cloud Portal Office Desktop\icons\CP_Red_Office_64.ico
UninstallDisplayIcon={app}\icons\CP_Red_Office_64.ico
UninstallDisplayName=Cloud Portal Office Desktop
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes
ChangesAssociations=yes
;ArchitecturesAllowed=x86 x64
  ;If the machine is x64, then install in x64 bit mode; Else in 32 bit mode
ArchitecturesInstallIn64BitMode=x64
PrivilegesRequired=admin
AlwaysRestart=yes
;SignTool=MSSignTool 


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[InstallDelete]
      ;Before Installation: The following lines will delete the Nuxeo's DB file and the directory
Type: files; Name: "{sd}\Users\{username}\.nuxeo-drive\nxdrive.db"; 
Type: filesandordirs; Name: "{sd}\Users\{username}\.nuxeo-drive";   
             ;  The following 2 lines will delete the 2 short cuts created by the installer
Type: files; Name: "{sd}\Users\{username}\AppData\Roaming\Microsoft\Windows\Start Menu\Programs\Startup\CpoDesktop.lnk";   
Type: files; Name: "{sd}\Users\{username}\Links\Cloud Portal Office.lnk";

[UninstallDelete]
      ;During Uninstall: The following lines will delete the Nuxeo's DB file and the directory
Type: files; Name: "{sd}\Users\{username}\.nuxeo-drive\nxdrive.db"; 
Type: filesandordirs; Name: "{sd}\Users\{username}\.nuxeo-drive";   
             ;  The following 2 lines will delete the 2 short cuts created by the installer
Type: files; Name: "{sd}\Users\{username}\AppData\Roaming\Microsoft\Windows\Start Menu\Programs\Startup\CpoDesktop.lnk";   
Type: files; Name: "{sd}\Users\{username}\Links\Cloud Portal Office.lnk";
Type: filesandordirs; Name: "{app}";   


[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; 

[Files]
Source: "Cloud Portal Office Desktop\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs 
Source: "BatchFiles\*"; DestDir: "{app}\BatchFiles\"; Flags: ignoreversion recursesubdirs createallsubdirs uninsrestartdelete
Source: "RedistPackages\*"; DestDir: "{app}\RedistPackages\"; Flags: ignoreversion recursesubdirs uninsrestartdelete;  
;If 64 bit installation
Source: "dll\64bit\CpoIconOverlayConflicted.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion uninsrestartdelete  ;  Check: Is64BitInstallMode
Source: "dll\64bit\CpoIconOverlayInProgress.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion uninsrestartdelete;  Check: Is64BitInstallMode
Source: "dll\64bit\CpoIconOverlaySynced.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion  uninsrestartdelete;  Check: Is64BitInstallMode
;If 32 bit installation
Source: "dll\32bit\CpoIconOverlayConflicted.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion uninsrestartdelete;  Check: not Is64BitInstallMode
Source: "dll\32bit\CpoIconOverlayInProgress.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion uninsrestartdelete;  Check: not Is64BitInstallMode
Source: "dll\32bit\CpoIconOverlaySynced.dll"; DestDir: "{app}\bin\"; Flags: ignoreversion uninsrestartdelete ;  Check: not Is64BitInstallMode

[Icons]                                                                                                        
Name: "{group}\Cloud Portal Office Desktop"; Filename: "{app}\CpoDesktop.exe"
Name: "{group}\{cm:UninstallProgram,Cloud Portal Office Desktop}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\Cloud Portal Office Desktop"; Filename: "{app}\CpoDesktop.exe"; Tasks: desktopicon
;Name: "{userstartup}\Cloud Portal Office Desktop"; Filename: "{app}\CpoDesktop.exe"   

[Run]
Filename: "{app}\RedistPackages\64bit\cpo_x64Setup.exe"; Description: "{cm:LaunchProgram, Install Redist packages - 64bit}"; Flags: waituntilterminated ; Check: Is64BitInstallMode
Filename: "{app}\RedistPackages\32bit\cpo_x86Setup.exe"; Description: "{cm:LaunchProgram, Install Redist packages - 32bit}"; Flags: waituntilterminated ; Check: not Is64BitInstallMode
Filename: "{app}\BatchFiles\RegisterSynchDLL.bat"; Parameters:"""{app}""" ; Description: "{cm:LaunchProgram,Register Synch DLL}"; Flags: nowait runhidden 64bit ; Check: Is64BitInstallMode
Filename: "{app}\BatchFiles\RegisterSynchDLL.bat"; Parameters:"""{app}""" ; Description: "{cm:LaunchProgram,Register Synch DLL}"; Flags: nowait runhidden ; Check: not Is64BitInstallMode
Filename: "{app}\CpoDesktop.exe"; Description: "{cm:LaunchProgram,Cloud Portal Office Desktop}"; Flags: nowait postinstall skipifsilent 

[UninstallRun]
Filename: "{app}\BatchFiles\UnregisterSynchDLL.bat"; Parameters:"""{app}""" ; Flags: nowait runhidden 64bit; Check: Is64BitInstallMode
Filename: "{app}\BatchFiles\UnregisterSynchDLL.bat"; Parameters:"""{app}""" ; Flags: nowait runhidden ; Check: not Is64BitInstallMode

[Registry]
Root: HKCU; Subkey: "Software\SHARP\CLOUD PORTAL OFFICE Desktop"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\SHARP\CpoDesktop\preferences"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\SHARP\CpoDesktop"; Flags: uninsdeletekey


[Code]
procedure Alert(const Text: String);
begin
     //MsgBox(Text, mbInformation, MB_OK)  ;
end;

procedure StopApplication();
var
  ResultCode: integer;
begin
    Alert('Stopping Application') ;
    if Is64BitInstallMode()  then
      ShellExec( 'open', 'taskkill.exe', '/F /T /IM CPODesktop.exe', '', SW_HIDE, ewNoWait, ResultCode) ;       //For Win 7
    if Is64BitInstallMode()  then
      ShellExec( 'open', 'tskill.exe', ' CPODesktop.exe /a /v ', '', SW_HIDE, ewNoWait, ResultCode) ;    //For WinXP
    if Not Is64BitInstallMode()  then
      Exec( 'taskkill.exe', '/F /T /IM CPODesktop.exe', '', SW_HIDE, ewNoWait, ResultCode) ;       //For Win 7
    if Not Is64BitInstallMode()  then
      Exec( 'tskill.exe', ' CPODesktop.exe /a /v ', '', SW_HIDE, ewNoWait, ResultCode) ;    //For WinXP
end;     

function InitializeSetup(): Boolean;
begin
  StopApplication();
  result := True;
end;
 
function InitializeUninstall(): Boolean;
begin
  StopApplication();
  result := True;
end;

